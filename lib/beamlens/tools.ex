defmodule Beamlens.Tools do
  @moduledoc """
  Tool response structs and union schema for the agent loop.

  Each struct represents a possible tool selection from the LLM.
  The union schema parses raw BAML responses into typed structs.
  """

  defmodule GetOverview do
    @moduledoc false
    defstruct [:intent]
  end

  defmodule GetSystemInfo do
    @moduledoc false
    defstruct [:intent]
  end

  defmodule GetMemoryStats do
    @moduledoc false
    defstruct [:intent]
  end

  defmodule GetProcessStats do
    @moduledoc false
    defstruct [:intent]
  end

  defmodule GetSchedulerStats do
    @moduledoc false
    defstruct [:intent]
  end

  defmodule GetAtomStats do
    @moduledoc false
    defstruct [:intent]
  end

  defmodule GetPersistentTerms do
    @moduledoc false
    defstruct [:intent]
  end

  defmodule GetTopProcesses do
    @moduledoc false
    defstruct [:intent, :limit, :offset, :sort_by]
  end

  defmodule Done do
    @moduledoc false
    defstruct [:intent, :analysis]
  end

  @doc """
  Returns a ZOI union schema for parsing SelectTool responses into structs.

  Uses discriminated union pattern matching on the `intent` field.
  """
  def schema do
    Zoi.union([
      tool_schema(GetOverview, "get_overview"),
      tool_schema(GetSystemInfo, "get_system_info"),
      tool_schema(GetMemoryStats, "get_memory_stats"),
      tool_schema(GetProcessStats, "get_process_stats"),
      tool_schema(GetSchedulerStats, "get_scheduler_stats"),
      tool_schema(GetAtomStats, "get_atom_stats"),
      tool_schema(GetPersistentTerms, "get_persistent_terms"),
      top_processes_schema(),
      done_schema()
    ])
  end

  defp tool_schema(module, intent_value) do
    Zoi.object(%{intent: Zoi.literal(intent_value)})
    |> Zoi.transform(fn data -> {:ok, struct!(module, data)} end)
  end

  defp top_processes_schema do
    Zoi.object(%{
      intent: Zoi.literal("get_top_processes"),
      limit: Zoi.integer() |> Zoi.optional(),
      offset: Zoi.integer() |> Zoi.optional(),
      sort_by: Zoi.enum(["memory", "message_queue", "reductions"]) |> Zoi.optional()
    })
    |> Zoi.transform(fn data -> {:ok, struct!(GetTopProcesses, data)} end)
  end

  defp done_schema do
    Zoi.object(%{
      intent: Zoi.literal("done"),
      analysis: Beamlens.HealthAnalysis.schema()
    })
    |> Zoi.transform(fn data -> {:ok, struct!(Done, data)} end)
  end
end
